plugins {
    id 'com.android.application'
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def AdMobAppId = localProperties.getProperty('AdMobAppId')
if (AdMobAppId == null) {
    AdMobAppId = 'ca-app-pub-3940256099942544~3347511713'
}

apply plugin: 'com.google.gms.google-services'

android {
    compileSdkVersion 34
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.appsbay.fourgreatclassicalnovels"
        minSdkVersion 21
        targetSdkVersion 34
        versionCode 6
        versionName "1.1.3"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        manifestPlaceholders = [AdMobAppId: AdMobAppId]
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            resValue "string", "adBannerID", localProperties['adBannerID'] ?: ""
            resValue "string", "adInterstitialID", localProperties['adInterstitialID'] ?: ""
        }

        debug {
            resValue "string", "adBannerID", "ca-app-pub-3940256099942544/6300978111"
            resValue "string", "adInterstitialID", "ca-app-pub-3940256099942544/1033173712"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.gridlayout:gridlayout:1.0.0'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    implementation 'com.google.android.gms:play-services-ads:22.2.0'
    implementation 'com.github.li-xiaojun:XPopup:2.10.0'

    implementation platform('com.google.firebase:firebase-bom:32.0.0')
    implementation 'com.google.firebase:firebase-analytics'

    def lifecycle_version = "2.0.0"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime:$lifecycle_version"
    annotationProcessor "androidx.lifecycle:lifecycle-compiler:$lifecycle_version"

    implementation 'com.google.ads.mediation:facebook:6.8.0.0'
}